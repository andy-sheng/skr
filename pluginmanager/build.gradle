apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            java.srcDirs = ['src/main/java', 'src/main/aidl']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
    lintOptions {
        abortOnError false
    }

}

dependencies {
    provided 'com.android.support:appcompat-v7:25.0.1'
    provided files('libs/EventBus-2.4.0.jar')
}

task makeJar(type: Jar, dependsOn: build) {
    delete('build/libs/pushsdk-noproguard.jar')
    //指定生成的jar名
    baseName 'liveplugin-noproguard'
    //从哪里打包class文件 ,打的包可以在build/libs中找到
    from('build/intermediates/classes/debug/', configurations.compile.collect {
        it.isDirectory() ? it : zipTree(it)
    })
    exclude '**/BuildConfig.class'
    exclude '**/R.class'
    exclude '**/R$*.class'
}

task proguardJar(type: proguard.gradle.ProGuardTask, dependsOn: makeJar) {
    delete('build/libs/liveplugin.jar')
    // 依赖库
    def sdkDir = android.getSdkDirectory().getAbsolutePath()
    if (sdkDir == null) {
        sdkDir = android.plugin.getSdkFolder().getAbsolutePath()
    }
    libraryjars sdkDir + '/platforms/' + android.compileSdkVersion.toString() + '/android.jar'
    libraryjars files('libs/EventBus-2.4.0.jar')
    // 输入路径
    injars 'build/libs/liveplugin-noproguard.jar'
    // 输出路径
    outjars 'build/libs/liveplugin.jar'
    // 添加配置信息
    configuration android.getDefaultProguardFile('proguard-android.txt')
    configuration 'proguard-rules.pro'
}

task buildJar(type: Copy, dependsOn: proguardJar) {
    from 'build/libs/liveplugin.jar'
    into '../plugindemo/libs'
}