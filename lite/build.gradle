apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java', 'src/main/java-gen']
        }
    }

    useLibrary 'org.apache.http.legacy'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // 引擎相关
    compile files('libs/broadcaster.jar')
    compile files('libs/webrtc.jar')
    compile files('libs/devicemanager.jar')
    compile files('libs/xplatform_util.jar')
    compile files('libs/player.jar')

    compile files('libs/milinksdk-3.1.4.jar')
    compile files('libs/protobuf-java-2.6.0.jar')

    // eventbus
    compile files('libs/eventbus-3.0.0.jar')
    // greendao
    compile files('libs/greendao-2.0.0.jar')

    compile files('libs/slf4j-api-1.7.21.jar')
    compile files('libs/rxandroid-1.1.0.jar')
    compile files('libs/rxjava-1.0.14.jar')
    compile files('libs/rxlifecycle-0.4.0.jar')

    provided 'com.android.support:appcompat-v7:25.3.1'
    provided 'com.android.support:support-annotations:25.3.1'
}

task makeJar(type: Jar, dependsOn: build) {
    delete('build/libs/livelite-noproguard.jar')
    //指定生成的jar名
    baseName 'livelite-noproguard'
    //从哪里打包class文件 ,打的包可以在build/libs中找到
    from('build/intermediates/classes/debug/', configurations.compile.collect {
        it.isDirectory() ? it : zipTree(it)
    })
    exclude '**/BuildConfig.class'
    exclude '**/R.class'
    exclude '**/R$*.class'
}

task proguardJar(type: proguard.gradle.ProGuardTask, dependsOn: makeJar) {
    delete('build/libs/livelite.jar')
    // 依赖库
    def sdkDir = android.getSdkDirectory().getAbsolutePath()
    if (sdkDir == null) {
        sdkDir = android.plugin.getSdkFolder().getAbsolutePath()
    }
    libraryjars sdkDir + '/platforms/' + android.compileSdkVersion.toString() + '/android.jar'
    // 输入路径
    injars 'build/libs/livelite-noproguard.jar'
    // 输出路径
    outjars 'build/libs/livelite.jar'
    // 添加配置信息
    configuration android.getDefaultProguardFile('proguard-android.txt')
    configuration 'proguard-rules.pro'
}

task buildJar(type: Copy, dependsOn: proguardJar) {
    from 'build/libs/livelite.jar'
    into '../litedemo/libs'
}
