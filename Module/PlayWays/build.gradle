if (isBuildModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
    apply plugin: 'org.greenrobot.greendao' // apply plugin
}

//kotlin-android用于编译Android代码
apply plugin: 'kotlin-android'
//kotlin-android-extensions用于扩展绑定关系.
apply plugin: 'kotlin-android-extensions'
//kotlin-kapt 需要使用kapt替换原来的annotationProcessor等complie方式
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        testInstrumentationRunner rootProject.ext.dependencies["androidJUnitRunner"]
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
                includeCompileClasspath true
            }
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

//    resourcePrefix "channel_" //给 Module 内的资源名增加前缀, 避免资源名冲突

    lintOptions {
        disable 'InvalidPackage'
        disable "ResourceType"
        abortOnError false
    }

    sourceSets {
        main {
            if (isBuildModule.toBoolean()) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
            }
        }

        main {
            java.srcDirs += 'src/main/java-gen-db'
            java.srcDirs += 'src/main/java-gen-pb'

            java.srcDirs += 'src/main/java'
            res.srcDirs += 'src/main/res'

            java.srcDirs += 'src/main/java-songselect'
            res.srcDirs += 'src/main/res-songselect'

            java.srcDirs += 'src/main/java-prepare'
            res.srcDirs += 'src/main/res-prepare'

            java.srcDirs += 'src/main/java-audition'
            res.srcDirs += 'src/main/res-audition'

            java.srcDirs += 'src/main/java-room-rank'
            res.srcDirs += 'src/main/res-room-rank'

            java.srcDirs += 'src/main/java-room-grab'
            res.srcDirs += 'src/main/res-room-grab'

            java.srcDirs += 'src/main/java-room-voice'
            res.srcDirs += 'src/main/res-room-voice'

            java.srcDirs += 'src/main/java-gift'
            res.srcDirs += 'src/main/res-gift'

            java.srcDirs += 'src/main/java-beauty'
            res.srcDirs += 'src/main/res-beauty'

            java.srcDirs += 'src/main/java-room-double'
            res.srcDirs += 'src/main/res-room-double'

            java.srcDirs += 'src/main/java-songmanage'
            res.srcDirs += 'src/main/res-songmanage'
        }
    }
}

dependencies {
    if (isBuildModule.toBoolean()) {
//        tools
        kapt rootProject.ext.dependencies["arouter-compiler"]
    } else {
        kapt rootProject.ext.dependencies["arouter-compiler"]
    }
    //因为 CommonRes 依赖了 CommonSDK, 所以如果业务模块需要公共 UI 组件就依赖 CommonRes, 如果不需要就只依赖 CommonSDK
    api project(":Component:BusiLib")
}

//与dependencies平级，主要用于配置数据库相关信息的及自动生成的“DAO”资源的目录
greendao {

    schemaVersion 6//数据库版本号

    daoPackage 'com.module.playways.db'//设置DaoMaster、DaoSession、Dao包名

    targetGenDir 'src/main/java-gen-db'//设置DaoMaster、DaoSession、Dao目录

    //targetGenDirTest：设置生成单元测试目录

    //generateTests：设置自动生成单元测试用例

}
